package ar.edu.itba.kUltra;

import java_cup.runtime.*;
import org.objectweb.asm.Type;
import org.objectweb.asm.commons.GeneratorAdapter;
import ar.edu.itba.kUltra.nodes.*;
import ar.edu.itba.kUltra.symbols.*;
import org.slf4j.Logger;
import org.slf4j.LoggerFactory;


parser code {:
    private final static Logger LOGGER = LoggerFactory.getLogger(Parser.class);
    Scanner scanner;

  public Parser(Scanner scann, ComplexSymbolFactory sf) {
    super(scann,sf);
    scanner = scann;
  }

	public static void main(String args[]) throws Exception {
		ComplexSymbolFactory sf = new ComplexSymbolFactory();
		if (args.length==0) new Parser(new Scanner(System.in,sf),sf).parse();
		else new Parser(new Scanner(new java.io.FileInputStream(args[0]),sf),sf).parse();
	}
:}

terminal MINUS, PLUS, MULT, DIV, MOD, UMINUS; // UMINUS is a terminal JUST defined for precedence purposes
terminal AND, OR;
terminal LT, LTEQ, GT, GTEQ, EQEQ, NOTEQ, EQ;
terminal LPAREN, RPAREN;
terminal SEMICOLON, COMMA, COLON;
terminal INT, STR, VOID; // +++xcheck
terminal RETURN, DEF, END, IF, ELSE, WHILE;


terminal Integer INTEGER;
terminal String STRING, IDENTIFIER;
// terminal void VOID; +++xcheck


non terminal ProgramNode Program;
non terminal NodeList<MethodNode> MethodList;
non terminal MethodNode Method;
non terminal ReturnNode Return;
non terminal ParameterListSymbol Parameters;
non terminal String Type;
non terminal BodyNode StatementList;
non terminal StatementNode Statement;
non terminal ExpressionNode Expression;
non terminal VariableNode Variable;
non terminal DeclarationNode VarDeclaration;
non terminal AssignmentNode Assignment;
non terminal IfNode If;
non terminal WhileNode While;
non terminal MethodCallNode MethodCall;
non terminal NodeList<ExpressionNode> Arguments;
non terminal LiteralNode Literal;

precedence left AND, OR;
precedence left LT, LTEQ, GT, GTEQ, EQEQ, NOTEQ;
precedence left MINUS, PLUS;
precedence left MULT, DIV, MOD;
precedence left UMINUS;

start with Program;


Program                 ::=             MethodList:ml StatementList:sl {: RESULT = new ProgramNode(ml, sl); LOGGER.debug("{}", RESULT); :}
                                        ;


MethodList              ::=             Method:m MethodList:ml {: ml.add(m); RESULT = ml; LOGGER.debug("{}", RESULT); :}
                                        | /* lambda */ {: RESULT = new NodeList<>(); LOGGER.debug("{}", RESULT); :}
                                        ;

Method                  ::=             DEF Type:t IDENTIFIER:i LPAREN Parameters:p RPAREN COLON StatementList:sl END
                     {: RESULT = new MethodNode(t, i, p, sl); LOGGER.debug("{}", RESULT); :}
                                        ;

Return                  ::=             RETURN Expression:e SEMICOLON {: RESULT = new ReturnNode(e); LOGGER.debug("{}", RESULT); :}
                                        | RETURN SEMICOLON {: RESULT = new ReturnNode(null); LOGGER.debug("{}", RESULT); :} // return; for void methods
                                        ;

Parameters              ::=             Type:t IDENTIFIER:i COMMA Parameters:p {: p.addParameter(t, i); RESULT = p; LOGGER.debug("{}", RESULT); :}
                                        | Type:t IDENTIFIER:i {: RESULT = new ParameterListSymbol(); RESULT.addParameter(t, i); LOGGER.debug("{}", RESULT); :}
                                        | /* lambda */ {: RESULT = new ParameterListSymbol(); LOGGER.debug("{}", RESULT); :}
                                        ;

Type                    ::=             INT {: RESULT ="Integer"; LOGGER.debug("{}", RESULT); :}
                                        | STR {: RESULT ="String"; LOGGER.debug("{}", RESULT); :}
                                        | VOID {: RESULT ="void"; LOGGER.debug("{}", RESULT); :}
                                        ;

StatementList           ::=             Statement:s StatementList:sl  {: sl.add(s); RESULT = sl; LOGGER.debug("{}", RESULT); :}
                                        | /* lambda */ {: RESULT = new BodyNode(); LOGGER.debug("{}", RESULT); :}
                                        ;

Statement               ::=             VarDeclaration:vd {: RESULT = vd; LOGGER.debug("Statement - {}", RESULT); :}
                                        | Assignment:a {: RESULT = a; LOGGER.debug("Statement - {}", RESULT); :}
                                        | If:i {: RESULT = i; LOGGER.debug("Statement - {}", RESULT); :}
                                        | While:w {: RESULT = w; LOGGER.debug("Statement - {}", RESULT); :}
                                        | MethodCall:mc {: RESULT = mc; LOGGER.debug("Statement - {}", RESULT); :} SEMICOLON
                                        | Return:r {: RESULT = r; LOGGER.debug("Statement - {}", RESULT); :}
                                        ;

Expression              ::=             Expression:e1 MINUS Expression:e2 {: RESULT = new ArithmeticNode(GeneratorAdapter.SUB, e1, e2); LOGGER.debug("{}", RESULT); :}
                                        | Expression:e1 PLUS Expression:e2 {: RESULT = new ArithmeticNode(GeneratorAdapter.ADD, e1, e2); LOGGER.debug("{}", RESULT); :}
                                        | Expression:e1 MULT Expression:e2 {: RESULT = new ArithmeticNode(GeneratorAdapter.MUL, e1, e2); LOGGER.debug("{}", RESULT); :}
                                        | Expression:e1 DIV Expression:e2 {: RESULT = new ArithmeticNode(GeneratorAdapter.DIV, e1, e2); LOGGER.debug("{}", RESULT); :}
                                        | Expression:e1 MOD Expression:e2 {: RESULT = new ArithmeticNode(GeneratorAdapter.REM, e1, e2); LOGGER.debug("{}", RESULT); :}
                                        | Expression:e1 AND Expression:e2 {: RESULT = new LogicalNode(GeneratorAdapter.AND, e1, e2); LOGGER.debug("{}", RESULT); :}
                                        | Expression:e1 OR Expression:e2 {: RESULT = new LogicalNode(GeneratorAdapter.OR, e1, e2); LOGGER.debug("{}", RESULT); :}
                                        | Expression:e1 LT Expression:e2 {: RESULT = new RelationalNode(Type.INT_TYPE, GeneratorAdapter.LT, e1, e2); LOGGER.debug("{}", RESULT); :}
                                        | Expression:e1 LTEQ Expression:e2 {: RESULT = new RelationalNode(Type.INT_TYPE, GeneratorAdapter.LE, e1, e2); LOGGER.debug("{}", RESULT); :}
                                        | Expression:e1 GT Expression:e2 {: RESULT = new RelationalNode(Type.INT_TYPE, GeneratorAdapter.GT, e1, e2); LOGGER.debug("{}", RESULT); :}
                                        | Expression:e1 GTEQ Expression:e2 {: RESULT = new RelationalNode(Type.INT_TYPE, GeneratorAdapter.GE, e1, e2); LOGGER.debug("{}", RESULT); :}
                                        | Expression:e1 EQEQ Expression:e2 {: RESULT = new RelationalNode(Type.INT_TYPE, GeneratorAdapter.EQ, e1, e2); LOGGER.debug("{}", RESULT); :}
                                        | Expression:e1 NOTEQ Expression:e2 {: RESULT = new RelationalNode(Type.INT_TYPE, GeneratorAdapter.NE, e1, e2); LOGGER.debug("{}", RESULT); :}
                                        | MINUS Expression:e {: RESULT = new ArithmeticNode(GeneratorAdapter.SUB, new LiteralNode<>(0), e); LOGGER.debug("{}", RESULT); :} /* +++ximprove */ %prec UMINUS
                                        | LPAREN Expression:e RPAREN {: RESULT = e; LOGGER.debug("Expression - {}", RESULT); :}
                                        | MethodCall:mc {: RESULT = mc; LOGGER.debug("Expression - {}", RESULT); :}
                                        | Literal:l {: RESULT = l; LOGGER.debug("Expression - {}", RESULT); :}
                                        | Variable:v {: RESULT = v; LOGGER.debug("Expression - {}", RESULT); :}
                                        ;


Variable                ::=             IDENTIFIER:i {: RESULT = new VariableNode(i); LOGGER.debug("{}", RESULT); :}
                                        ;

VarDeclaration          ::=             Type:t IDENTIFIER:i SEMICOLON {: RESULT = new DeclarationNode(t, i); LOGGER.debug("{}", RESULT); :}
                                        ;

Assignment              ::=             IDENTIFIER:i EQ Expression:e SEMICOLON {: RESULT = new AssignmentNode(i, e); LOGGER.debug("{}", RESULT); :}
                                        ;

If                      ::=             IF LPAREN Expression:c RPAREN COLON StatementList:sl END {: RESULT = new IfNode(c, sl, null); LOGGER.debug("{}", RESULT); :}
                                        |
                                        IF LPAREN Expression:c RPAREN COLON StatementList:sl ELSE COLON StatementList:el END {: RESULT = new IfNode(c, sl, el); LOGGER.debug("{}", RESULT); :}
                                        ;

While                   ::=             WHILE LPAREN Expression:c RPAREN COLON StatementList:sl END {: RESULT = new WhileNode(c, sl); LOGGER.debug("{}", RESULT); :}
                                        ;

MethodCall              ::=             IDENTIFIER:i LPAREN Arguments:a RPAREN {: RESULT = new MethodCallNode(i, a); LOGGER.debug("{}", RESULT); :}
                                        ;

Arguments               ::=             Expression:e COMMA Arguments:a {: a.add(e); RESULT = a; LOGGER.debug("{}", RESULT); :}
                                        | Expression:e {: RESULT = new NodeList<>(); RESULT.add(e); LOGGER.debug("{}", RESULT); :}
                                        | /* lambda */ {: RESULT = new NodeList<>(); LOGGER.debug("{}", RESULT); :}
                                        ;

Literal                 ::=             INTEGER:n {: RESULT = new LiteralNode<Integer>(n); LOGGER.debug("{}", RESULT); :}
                                        |
                                        STRING:s {: RESULT = new LiteralNode<String>(s); LOGGER.debug("{}", RESULT); :};
                                        

